# Maintainer: Cthulu201 <cthulu201@gmail.com>
# Maintainer: Christopher Arndt <aur -at- chrisarndt -dot- de>

_pkgname='pigpio'
pkgname="python-${_pkgname}"
pkgver=77
pkgrel=1
groups=('mueditor')
pkgdesc="A C and Python library for controlling GPIOs on a Raspberry Pi"
url="https://github.com/joan2937/pigpio"
license=('custom:UNLICENSE')
arch=('i686' 'x86_64' 'aarch64' 'armv7h')
depends=('python')
makedepends=('make')
source=("${_pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha512sums=('0533d18bc244d4cf487ffebdd68b9f341139e70df3a3862dc4705afefa2e3e4e2f51792a6c4a440b8ee1f111c9ae3141770b9578601bfa44e166bb438f45b80c')


prepare() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  sed -e '/which python2/d' -i Makefile
  sed -e '/\/opt/d' -i Makefile
  sed -e 's|\$(prefix)/man|\$(prefix)/share/man|' -i Makefile
  sed -e 's|/usr/bin/pigpiod|/usr/bin/pigpiod -g|' -i util/pigpiod.service
}

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make prefix=/usr DESTDIR="${pkgdir}" install
  install -Dm644 util/pigpiod.service -t "${pkgdir}/usr/lib/systemd/system"
  install -Dm644 UNLICENCE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
