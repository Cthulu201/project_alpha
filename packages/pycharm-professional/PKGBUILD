# Maintainer: Cthulu201 <cthulu201@gmail.com>

_pkgname="pycharm"
pkgname="${_pkgname}-professional"
pkgver=2020.1.2
pkgrel=2
groups=('project_alpha' 'project_alpha-editors')
pkgdesc="Python IDE for Professional Developers. Professional Edition"
arch=('x86_64')
url='https://www.jetbrains.com/pycharm/'
conflicts=('pycharm' 'pycharm-community-edition')
provides=('pycharm')
license=('custom')
backup=(
    "opt/${pkgname}/bin/pycharm.vmoptions"
    "opt/${pkgname}/bin/pycharm64.vmoptions"
    "opt/${pkgname}/bin/idea.properties"
)
depends=('giflib' 'glibc' 'sh' 'libxtst' 'libxslt' 'python')
source=("https://download.jetbrains.com/python/${pkgname}-${pkgver}.tar.gz"
        "pycharm-professional.desktop"
        "pycharm"
        "charm.desktop"
        "charm"
        "PyCharm_Reference_Card.pdf")
sha512sums=('2ae316b50e086d3f0497f4a2f95cebfe67f4db00633f9988177f7c3957fd6ff3aea5273961bbd3761fe63a09e13e151c36a7db7bf7f876e27fbf5903f46164c0'
            '7eb569a85b5bbd620e264ac545ae02b1784a0d4296203d4a57a0d15216e526888ced471ae8734c8d24f710bf1ca688fcf64388cc0ef8f19f3019a384ae79bfad'
            'abe93aafc17990c856db6e3ee78e474429f0124cc8d0702db90caff2761a28b0c68f1ddc1c57a326e84fe78a14eb9e4acbfb0d8a845bb8af30f4e8f018e10a2d'
            '8db4d6fee525ee110a7a5403d30ed5a7aede63eae5f0ac3f86cd64600ff2ae20a6649ea8c12b54dffed43001e142af8c1a4e870821dc943810f41a2437d1c660'
            '72d736c7213c8c35cf2be79469afa4a0b6a36f6cf9dcfb8b948677bea2661dcc31b627b55700b5c02041da7fe7427b0a0925db0a1cd282a64d0b35995a39e5dd'
            'cecafd161446992b42d7f315364fad6e215f26de2e743555ba35f3a9a3ca5d2e707b1be71a56226a76faff7150858a42c3b733e33d2e6d61c882ab450bc9943c')
makedepends=('python-setuptools')
optdepends=('python2: Python 2 support'
            'ipython2: For enhanced interactive Python shell v2 inside Pycharm'
            'ipython: For enhanced interactive Python shell v3 inside Pycharm'
            'openssh: For deployment and remote connections'
            'python2-setuptools: Packages manager for Python 2, for project interpreter'
            'python-setuptools: Packages manager for Python 3, for project interpreter'
            'python2-coverage: For support code coverage measurement for Python 2'
            'python-coverage: For support code coverage measurement for Python 3'
            'cython2: For performance debugger in Python 2'
            'cython: For performance debugger in Python 3'
            'docker-machine: For support docker inside Pycharm'
            'docker-compose: For support docker inside Pycharm'
            'vagrant: For support virtualized development environments'
            'python2-pytest: For support testing inside Pycharm with Python 2'
            'python-pytest: For support testing inside Pycharm with Python 3'
            'python2-tox: Python environments for testing tool with Python 2'
            'python-tox: Python environments for testing tool with Python 3'
            'jupyter: For support Jupyter Notebook'
            'python-docutils-stubs: For build documentation with sphynx')
            
build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  # compile PyDev debugger used by PyCharm to speedup debugging
  python plugins/python/helpers/pydev/setup_cython.py build_ext --build-temp build --build-lib .
  
  rm -r bin/fsnotifier lib/pty4j-native/linux/{x86,ppc64le}
}

package() {
  # workaround FS#40934
  sed -i "s/lcd/on/" "${_pkgname}-${pkgver}/bin/"*.vmoptions

  # base
  install -dm 755 "${pkgdir}/opt/${pkgname}"
  cp -dr --no-preserve=ownership "${srcdir}/${_pkgname}-${pkgver}/"* "${pkgdir}/opt/${pkgname}/"
  install -dm 755 "${pkgdir}/usr/share/"{applications,pixmaps}
  install -Dm 644 "${pkgdir}/opt/${pkgname}/bin/${_pkgname}.png" "${pkgdir}/usr/share/pixmaps/${_pkgname}.png"
  install -Dm 644 "${pkgname}.desktop" "${pkgdir}/usr/share/applications/"
  
  # exec
  install -dm 755 "${pkgdir}/usr/bin/"
  install -Dm 755 "${_pkgname}" "${pkgdir}/usr/bin/"
  
  # licenses
  install -dm 755 "${pkgdir}/usr/share/licenses/${pkgname}/"
  cp -dr --no-preserve=ownership "${_pkgname}-${pkgver}/license/"* "${pkgdir}/usr/share/licenses/${pkgname}/"
  
  # install charm application - for edit a single file in Pycharm
  install -Dm 755 charm "${pkgdir}/usr/bin/"
  install -Dm 644 charm.desktop "${pkgdir}/usr/share/applications/"
}
