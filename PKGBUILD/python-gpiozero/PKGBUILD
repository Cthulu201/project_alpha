# Maintainer: Cthulu201 <cthulu201@gmail.com>
# Maintainer: Christopher Arndt <aur -at- chrisarndt -dot- de>

_pkgname=gpiozero
pkgname="python-${_pkgname}"
pkgver=1.5.1
pkgrel=2
pkgdesc='A simple interface to GPIO devices with Raspberry Pi'
arch=('any')
url="https://github.com/${_pkgname}/${_pkgname}"
license=('BSD')
optdepends=('python-spidev: for SPI access'
            'python-raspberry-gpio: for pin access via raspberry-gpio-python library')
depends=('python-colorzero' 'python-setuptools')
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/${_pkgname}/${_pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('73347ec887f6d6b58a09d8bfe604826df62cb9455fe2eab0de74e5130f7f9912a2f4caa25b1eaa80b50a5e1b1b4f712333facd1ff9405d53122f9602282fbf82')


build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}/" -O1 --skip-build
  install -Dm644 LICENSE.rst -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
